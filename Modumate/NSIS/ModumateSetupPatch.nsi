; Script generated by the HM NIS Edit Script Wizard.

Unicode True
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Modumate"

#Note: product version expected as input parameter
#!define PRODUCT_VERSION "UnSetVersion"

!define PRODUCT_PUBLISHER "Modumate, Inc"
!define PRODUCT_WEB_SITE "https://www.modumate.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Modumate.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_URL_ROOT_KEY "HKEY_CLASSES_ROOT"
!define PRODUCT_URL_KEY "mdmt"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\Build\Windows\Application.ico"
!define MUI_UNICON "..\Build\Windows\Application.ico"

!define MUI_TEXT_WELCOME_INFO_TEXT "Setup will update the installed Modumate from v${PREVIOUS_VERSION} to v${PRODUCT_VERSION}.$\r$\n$\r$\n$_CLICK"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\Build\EULA.rtf"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\Modumate.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

!define PATH_OUT "${PATH_OUT_ROOT}/${PRODUCT_VERSION}"
!system 'md "${PATH_OUT}"'

!finalize '.\SignInstaller.bat "%1"'

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${PATH_OUT}\ModumatePatchSetup.exe"
InstallDir "$PROGRAMFILES64\Modumate"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

!define PATCH_INSTALL_ROOT "$INSTDIR"
!define PATCH_SOURCE_ROOT "${BUILD_DIR}\..\Release\${PRODUCT_VERSION}\WindowsNoEditor"
!define PATCH_FILES_ROOT "${BUILD_DIR}\patchFiles"
!include "${BUILD_DIR}\patchFiles.nsi"

Section "Patch Install Core"
  SectionIn RO
  SetOutPath $INSTDIR

  Call patchFilesRemoved
  Call patchDirectoriesRemoved
  Call patchDirectoriesAdded
  Call patchFilesAdded
  Call patchFilesModified
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Modumate\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Modumate\Uninstall.lnk" "$INSTDIR\UninstallModumate.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\UninstallModumate.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Modumate.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\UninstallModumate.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\Modumate.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}" "URL protocol" ""
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}" "" "URL:Modumate protocol"
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}\DefaultIcon" "" "$INSTDIR\Modumate.exe"
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}\shell" "" ""
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}\shell\open" "" ""
  WriteRegStr ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}\shell\open\command" "" '"$INSTDIR\Modumate.exe" -CustomURL="%1"'
  Delete "$INSTDIR\Modumate\Binaries\Win64\PDFNetDotNetCore.dll"
  Delete "$INSTDIR\Modumate\Binaries\Win64\PDFNetC.dll"
  Delete "$INSTDIR\Modumate\Binaries\Win64\Modumate-Win64-Shipping.pdb"
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$SMPROGRAMS\Modumate\Uninstall.lnk"
  Delete "$SMPROGRAMS\Modumate\Website.lnk"
  Delete "$DESKTOP\Modumate.lnk"
  Delete "$SMPROGRAMS\Modumate\Modumate.lnk"

  RMDir "$SMPROGRAMS\Modumate"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey ${PRODUCT_URL_ROOT_KEY} "${PRODUCT_URL_KEY}"
  SetAutoClose true
SectionEnd
